buildscript {
  if (project == rootProject) {
    repositories {
      google()
      mavenCentral()
      jcenter()
    }

    dependencies {
      classpath 'com.android.tools.build:gradle:3.5.3'
    }
  }
}

apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
  // See https://github.com/facebookincubator/fbjni/issues/50
  buildFeatures {
    prefab true //important for JSI
  }
  dependencies {
    implementation 'com.facebook.fbjni:fbjni:0.2.2' //important for JSI
  }
  configurations {
    all*.exclude module: 'fbjni-java-only' //important for JSI
  }

  compileSdkVersion safeExtGet('JsiTestView_compileSdkVersion', 29)
  defaultConfig {
    minSdkVersion safeExtGet('JsiTestView_minSdkVersion', 16)
    targetSdkVersion safeExtGet('JsiTestView_targetSdkVersion', 29)
    versionCode 1
    versionName "1.0"

    externalNativeBuild {
      cmake {
        cppFlags "-O2 -frtti -fexceptions -Wall -fstack-protector-all"
        abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        arguments "-DANDROID_STL=c++_shared" //important for JSI
      }
    }

  }

  externalNativeBuild {
    cmake {
      path "CMakeLists.txt"
    }
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }
  lintOptions {
    disable 'GradleCompatible'
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

repositories {
  mavenLocal()
  maven {
    // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
    url("$rootDir/../node_modules/react-native/android")
  }
  google()
  mavenCentral()
  jcenter()
}

dependencies {
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native:+"  // From node_modules
}
